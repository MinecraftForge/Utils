plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    alias libs.plugins.licenser
    alias libs.plugins.gradleutils
}

final projectDisplayName = 'Download Utils'
final projectVendor = 'Forge Development LLC'
description = 'Utilities for simple download tasks'
group = 'net.minecraftforge'
version = gitversion.tagOffset

println "Version: $version"

// IDE linting + no need to say 'sourceSets.java11' every time
final java11 = sourceSets.create('java11')

java {
    toolchain.languageVersion = JavaLanguageVersion.of 8
    withSourcesJar()

    registerFeature(java11.name) {
        usingSourceSet java11
        capability project.group.toString(), project.name, project.version.toString()
        disablePublication()
    }
}

configurations {
    named(java11.implementationConfigurationName) { extendsFrom implementation }
    named(java11.compileOnlyConfigurationName) { extendsFrom compileOnly }
}

dependencies {
    implementation project(':log-utils')

    compileOnly libs.nulls
}

tasks.named('jar', Jar) {
    manifest {
        attributes([
            'Automatic-Module-Name': 'net.minecraftforge.utils.download',
            'Multi-Release'        : 'true'
        ])
        attributes([
            'Specification-Title'   : projectDisplayName,
            'Specification-Vendor'  : projectVendor,
            'Specification-Version' : gitversion.info.tag,
            'Implementation-Title'  : projectDisplayName,
            'Implementation-Vendor' : projectVendor,
            'Implementation-Version': project.version
        ], 'net/minecraftforge/util/download/')
    }

    into('META-INF/versions/11') {
        from java11.output
    }
}

tasks.named(java11.compileJavaTaskName, JavaCompile) {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of 11
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

license {
    header = rootProject.file('LICENSE-header.txt')
    newLine = false
    exclude '**/*.properties'
}

changelog {
    fromBase()
    publishAll = false
}

publishing {
    publications.register('mavenJava', MavenPublication).configure {
        from components.java

        artifactId = project.name

        pom { pom ->
            name = projectDisplayName
            description = project.description

            gradleutils.pom.setGitHubDetails pom

            licenses {
                license gradleutils.pom.licenses.LGPLv2_1
            }

            developers {
                developer gradleutils.pom.developers.LexManos
                developer gradleutils.pom.developers.Jonathing
            }
        }
    }

    repositories {
        maven gradleutils.publishingForgeMaven
    }
}

idea.module { downloadSources = downloadJavadoc = true }
