plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.licenser' version '1.1.1'
    id 'net.minecraftforge.gradleutils' version '2.4.13'

    // modules
    id 'org.gradlex.extra-java-module-info' version '1.8'
}

apply from: rootProject.file('build_shared.gradle')

dependencies {
    implementation project(':log-utils')

    implementation libs.srgutils
    implementation libs.commons.io

    // Static Analysis
    compileOnly libs.nulls
}

extraJavaModuleInfo {
    failOnMissingModuleInfo = false

    automaticModule(libs.srgutils, 'net.minecraftforge.srgutils')
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
    withSourcesJar()
}

jar {
    manifest {
        attributes([
                'Specification-Title':   'File Utils',
                'Specification-Vendor':  'Forge Development LLC',
                'Specification-Version':  gitversion.version.info.tag,
                'Implementation-Title':  'File Utils',
                'Implementation-Vendor': 'Forge Development LLC',
                'Implementation-Version': project.version
        ] as LinkedHashMap, 'net/minecraftforge/util/file/')
    }
}

publishing {
    publications.register('mavenJava', MavenPublication).configure {
        artifactId = project.name
        from components.java

        pom { pom ->
            name = 'File Utils'
            description = 'Common utilities for working with files and directories'

            gradleutils.pom.gitHubDetails = pom

            license gradleutils.pom.licenses.LGPLv2_1

            developers {
                developer gradleutils.pom.Developers.LexManos
                developer gradleutils.pom.Developers.Jonathing
            }
        }
    }
    repositories {
        maven gradleutils.publishingForgeMaven
    }
}
