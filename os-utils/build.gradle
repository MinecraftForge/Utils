plugins {
    id 'java-library'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    alias libs.plugins.licenser
    alias libs.plugins.gradleutils
    alias libs.plugins.gitversion
    alias libs.plugins.changelog
}

gradleutils.displayName = 'OS Utils'
description = 'Utilities for identifying operating systems, architectures, and their defaults.'
group = 'net.minecraftforge'
version = gitversion.tagOffset

println "Version: $version"

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withSourcesJar()
    //withJavadocJar()
}

dependencies {
    compileOnly libs.nulls
}

tasks.named('jar', Jar) {
    manifest {
        attributes('Automatic-Module-Name': 'net.minecraftforge.utils.os')

        gradleutils.manifestDefaults(it, 'net/minecraftforge/util/os/')
    }
}

license {
    header = rootProject.file('LICENSE-header.txt')
    newLine = false
    exclude '**/*.properties'
}

changelog {
    fromBase()
}

publishing {
    repositories {
        maven gradleutils.publishingForgeMaven
    }

    publications.register('mavenJava', MavenPublication) {
        from components.java

        changelog.publish(it)
        gradleutils.promote(it)

        pom { pom ->
            name = gradleutils.displayName
            description = project.description

            gradleutils.pom.addRemoteDetails(pom)

            licenses {
                license gradleutils.pom.licenses.LGPLv2_1
            }

            developers {
                developer gradleutils.pom.developers.LexManos
                developer gradleutils.pom.developers.Jonathing
            }
        }
    }
}
