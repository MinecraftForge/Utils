plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.licenser' version '1.1.1'
    id 'net.minecraftforge.gradleutils' version '2.4.13'
}

apply from: rootProject.file('build_shared.gradle')

java {
    toolchain.languageVersion = JavaLanguageVersion.of(6)
    withSourcesJar()
    withJavadocJar()
}

jar {
    manifest {
        attributes('Automatic-Module-Name': 'net.minecraftforge.utils.logging')
        attributes([
            'Specification-Title':   'Logging Utils',
            'Specification-Vendor':  'Forge Development LLC',
            'Specification-Version':  gitversion.version.info.tag,
            'Implementation-Title':  'Logging Utils',
            'Implementation-Vendor': 'Forge Development LLC',
            'Implementation-Version': project.version
        ] as LinkedHashMap, 'net/minecraftforge/util/logging/')
    }
}

publishing {
    publications.register('mavenJava', MavenPublication).configure {
        artifactId = project.name
        from components.java

        pom { pom ->
            name = 'Logging Utils'
            description = 'General-purpose logging utilities for Forge-specific projects'

            gradleutils.pom.gitHubDetails = pom

            license gradleutils.pom.licenses.LGPLv2_1

            developers {
                developer gradleutils.pom.Developers.LexManos
                developer gradleutils.pom.Developers.Jonathing
            }
        }
    }
    repositories {
        maven gradleutils.publishingForgeMaven
    }
}
