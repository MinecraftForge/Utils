import org.gradle.api.plugins.jvm.JvmTestSuite
import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id 'java-library'
    id 'jvm-test-suite'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    alias libs.plugins.licenser
    alias libs.plugins.gradleutils
    alias libs.plugins.gitversion
    alias libs.plugins.changelog
}

gradleutils.displayName = 'Logging Utils'
description = 'General-purpose logging utilities for Forge-specific projects'
group = 'net.minecraftforge'
version = gitversion.tagOffset

println "Version: $version"

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withSourcesJar()
}

dependencies {
    compileOnly libs.nulls
}

testing {
    suites.named('test', JvmTestSuite) {
        useJUnitJupiter()

        targets.configureEach {
            testTask.configure {
                // TODO [LogUtils][Testing] Remove once proper tests are in place
                failOnNoDiscoveredTests = false
            }
        }
    }
}

tasks.named('jar', Jar) {
    manifest {
        attributes('Automatic-Module-Name': 'net.minecraftforge.utils.logging')

        gradleutils.manifestDefaults(it, 'net/minecraftforge/util/logging/')
    }
}

license {
    header = rootProject.file('LICENSE-header.txt')
    newLine = false
    exclude '**/*.properties'
}

changelog {
    fromBase()
}

publishing {
    repositories {
        maven gradleutils.publishingForgeMaven
    }

    publications.register('mavenJava', MavenPublication) {
        from components.java

        changelog.publish(it)
        gradleutils.promote(it)

        pom { pom ->
            name = gradleutils.displayName
            description = project.description

            gradleutils.pom.addRemoteDetails(pom)

            licenses {
                license gradleutils.pom.licenses.LGPLv2_1
            }

            developers {
                developer gradleutils.pom.developers.LexManos
                developer gradleutils.pom.developers.Jonathing
            }
        }
    }
}
